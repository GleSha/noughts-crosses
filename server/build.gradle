plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'java'
    id 'org.asciidoctor.convert' version "1.5.9.2"
    id 'jacoco'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'

sourceCompatibility = 11

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
//    implementation(project(':ui'))
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    asciidoctor 'io.spring.asciidoctor:spring-asciidoctor-extensions:0.1.3.RELEASE'
}

asciidoctor {
    sourceDir 'src/main/asciidoc'
    outputDir = file("${project.buildDir}/docs")
    options doctype: 'book'
    attributes 'snippets': file('build/snippets'),
            'source-highlighter': 'coderay',
            'toc': 'left',
            'toc-title': 'Table of Contents',
            'toclevels': '5',
            'icons': 'font',
            'icon-set': 'fa',
            'setanchors': 'true',
            'idprefix': '',
            'idseparator': '-',
            'last-update-label!': '',
            'nofooter': 'true',
            'pdf-page-size': 'A4',
            'pdf-style': 'custom'
}

asciidoctor.dependsOn(test)

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

repositories {
    maven { url "https://repo.spring.io/release" }
}
